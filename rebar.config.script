%%-*- mode: erlang -*-

%% ==============================================================================
%% maybe upload coverdata
%% ==============================================================================

CONFIG1 = case os:getenv("TRAVIS") of
              "true" ->
                  JobId = os:getenv("TRAVIS_JOB_ID"),
                  [{coveralls_service_job_id, JobId},
                   {coveralls_coverdata, "_build/test/cover/*.coverdata"},
                   {coveralls_service_name , "travis-ci"} | CONFIG];
              _ ->
                  CONFIG
          end,

%% ==============================================================================
%% Dependencies
%% ==============================================================================

Kf = fun(K, L) -> {K, V} = lists:keyfind(K, 1, L), V end,
BaseDeps = Kf(deps, CONFIG1),
CloudDeps = BaseDeps ++ Kf(cloud_deps, CONFIG1),
EdgeDeps = BaseDeps ++ Kf(edge_deps, CONFIG1),

%% Make a dep element for rebar.config GitRef should be either {tag, Tag} or {branch, Branch}
MakeDep =
    fun({Name, {git, _, _}} = App, _DefaultDepRef) ->
        %% alreay a complete ref
        App;
       (App, DefaultDepRef) ->
        {AppName, GitRef} =
            case App of
                {Name, Pinned} when is_tuple(Pinned) -> {Name, Pinned};
                {Name, Tag} when is_list(Tag) -> {Name, {tag, Tag}};
                Name when is_atom(Name) -> {Name, DefaultDepRef}
           end,
        RepoName = string:join(string:tokens(atom_to_list(AppName), "_"), "-"),
        URL = "https://github.com/emqx/" ++ RepoName,
        {AppName, {git, URL, GitRef}}
    end,

MakeDeps = fun(Deps, DefaultDepRef, TestDeps) -> [MakeDep(App, DefaultDepRef) || App <- Deps] ++ TestDeps end,

%% TODO: this is only a temporary workaround in order to be backward compatible
%% The right way is to always pin dependency version in rebar.config
%% Any dependency that can not be tested and released independently
%% (i.e. has to be a part of a emqx release in order to be tested)
%% should not be a dependency but a local application reside in the same repo.
%% (Meaning: emqx should be an umbrella project)
DefaultDepRef =
    case os:getenv("EMQX_DEPS_DEFAULT_VSN") of
        false -> {branch, "develop"}; %% not set
        "" -> {branch, "develop"}; %% set empty
        MaybeTag ->
            case re:run(MaybeTag, "v\[0-9\]+\.\[0-9\]+\.*") of
                nomatch -> {branch, MaybeTag};
                _ -> {tag, MaybeTag}
            end
    end,

%% ==============================================================================
%% Relx configs
%% ==============================================================================

GitDescribe = [I || I <- os:cmd("git describe --tag --match \"v*\""), I =/= $\n],
Relx0 = Kf(relx, CONFIG1),
{release, _, RelxBaseApps} = lists:keyfind(release, 1, Relx0),

RelxOverlay = Kf(overlay, Relx0),
RelxCloudApps = RelxBaseApps ++ Kf(cloud_relx_apps, CONFIG1),
RelxEdgeApps = RelxBaseApps ++ Kf(edge_relx_apps, CONFIG1),
RelxCloudOverlay0 = Kf(cloud_relx_overlay, CONFIG1),
RelxEdgeOverlay0 = Kf(edge_relx_overlay, CONFIG1),
RelxCloudOverlay = RelxOverlay ++ RelxCloudOverlay0,
RelxEdgeOverlay = RelxOverlay ++ RelxEdgeOverlay0,

MakeRelx =
    fun(Apps, Overlay, Vars) ->
        VarFiles = ["vars-" ++ atom_to_list(Var) ++ ".config" || Var <- Vars],
        Apps1 = case os:type() of {win32, nt} -> Apps -- [bcrypt]; _Other -> Apps end,
        Relx1 = lists:keystore(release, 1, Relx0, {release, {emqx, GitDescribe}, Apps1}),
        Relx2 = lists:keystore(overlay, 1, Relx1, {overlay, Overlay}),
        lists:keystore(overlay_vars, 1, Relx2, {overlay_vars, VarFiles})
    end,
Relx = fun(Vars) -> MakeRelx(RelxBaseApps, RelxOverlay, Vars) end,
RelxCloud = fun(Vars) -> MakeRelx(RelxCloudApps, RelxCloudOverlay, Vars) end,
RelxEdge = fun(Vars) -> MakeRelx(RelxEdgeApps, RelxEdgeOverlay, Vars) end,

TestDeps = [ {meck, "0.8.13"} % hex
           , {bbmustache, "1.7.0"} % hex
           , {emqx_ct_helpers, {git, "https://github.com/emqx/emqx-ct-helpers", {tag, "v1.1.1"}}}
           ],

%% ==============================================================================
%% Profiles
%% ==============================================================================
Profiles =
[ {emqx, [ {deps, MakeDeps(CloudDeps, DefaultDepRef, [])}
         , {relx, RelxCloud([cloud, dev])}
         ]}
, {emqx_pkg, [ {deps, MakeDeps(CloudDeps, DefaultDepRef, [])}
             , {relx, RelxCloud([cloud, pkg])}
             ]}
, {emqx_edge, [ {deps, MakeDeps(EdgeDeps, DefaultDepRef, [])}
              , {relx, RelxEdge([edge, dev])}
              ]}
, {emqx_edge_pkg, [ {deps, MakeDeps(EdgeDeps, DefaultDepRef, [])}
                  , {relx, RelxEdge([edge, pkg])}
                  ]}
],

Deletes = [deps, relx, cloud_deps, cloud_relx_apps, cloud_relx_overlay],
Additions = [{profiles, Profiles}],

CONFIG2 = lists:foldl(fun(K, Acc) -> lists:keydelete(K, 1, Acc) end, CONFIG1, Deletes),
CONFIG3 = lists:foldl(fun({K, V}, Acc) -> lists:keystore(K, 1, Acc, {K, V}) end, CONFIG2, Additions),

FilePath = case os:type() of
               {win32, nt} ->
                   "emqx.rebar.config";
               _ ->
                   "/tmp/emqx.rebar.config"
           end,

file:write_file(FilePath, [io_lib:format("~p.\n", [I]) || I <- CONFIG3]),

CONFIG3.

